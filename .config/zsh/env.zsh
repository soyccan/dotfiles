# PATH
function {
    # null_glob: ignore error when globbing fails (/path/to/some/dir/* not exist)
    [[ -o null_glob ]] && local null_glob_set=1 || local null_glob_set=
    setopt null_glob

    local new_path=()
    local p

    # Recommended place for user-specific executable files by XDG Base Directory Specification
    # executables installed by pip are also here
    new_path+=$HOME/.local/bin

    # Snap
    new_path+=/snap/bin

    # Go
    new_path+=$HOME/go/bin

    # Haskell
    new_path+=($HOME/.ghcup/bin $HOME/.cabal/bin)

    # Ruby rvm & gems
    new_path+=$HOME/.rvm/bin
    for p in $HOME/.gem/ruby/*; do
        new_path+=$p/bin
    done

    # Python (pyenv)
    new_path+=$HOME/.pyenv/bin

    # Python (masOS)
    new_path+=(/Library/Frameworks/Python.framework/Versions/Current/bin)
    for p in $HOME/Library/Python/*/bin; do
        new_path+=$p
    done

    # Foundry
    new_path+=$HOME/.foundry/bin

    # update PATH env var
    for p in $new_path; do
        # if $p exists as a dir & not in current $PATH, prepend it to $PATH
        [[ -d $p && ! ${path[(r)$p]} ]] && path=($p $path)
    done

    [[ ! $null_glob_set ]] && unsetopt null_glob
}

# Man pages path
# export MANPATH="/usr/local/man:$MANPATH"

# Locales
# let Nix use the system locale archive
export LOCALE_ARCHIVE=/usr/lib/locale/locale-archive
# file generated by update-locale
[[ -f /etc/default/locale ]] && source /etc/default/locale
# LC_ALL overrides every $LANG and $LC_*, and should be only used for temporary testing
export LANGUAGE=${LANGUAGE:-${LANG}}
export LANG LANGUAGE LC_CTYPE LC_NUMERIC LC_TIME LC_COLLATE LC_MONETARY LC_MESSAGES LC_PAPER \
    LC_NAME LC_ADDRESS LC_TELEPHONE LC_MEASUREMENT LC_IDENTIFICATION

# Preferred editor for local and remote sessions
editors=(hx nvim vim vi nano)
for editor in $editors; do
    if zshrc_has $editor; then
        export EDITOR=$editor
        break
    fi
done

# Homebrew
zshrc_has brew && export HOMEBREW_NO_AUTO_UPDATE=1

# pagers
export LESSOPEN="|/bin/lesspipe %s"
export BAT_PAGER="/usr/bin/less -RFi" # ignore case

# ripgrep
export RIPGREP_CONFIG_PATH="${XDG_CONFIG_HOME:-$HOME/.config}/ripgrep/config"
